//@version=4
study(title="Order Blocks", overlay=true,max_bars_back=1000)

// Input options
range = input(title="Candle Range", type=input.integer,defval=15, minval=5, maxval=100, group="BASIC SETTINGS")
bearishOBColour = input(title="Bearish OB Colour", type=input.color,defval=color.rgb(255,0,0, 90), group="STYLES")
bullishOBColour = input(title="Bullish OB Colour", type=input.color,defval=color.rgb(0,255,0, 90), group="STYLES")

// candle colouring
var int CandleColourMode = 0
var bool BosCandle = false

// tracking for entries
var int lastDownIndex=0
var float lastDown=0
var float lastLow=0

var int lastUpIndex=0
var float lastUp=0
var float lastUpLow=0
var float lastUpOpen=0
var float lastHigh=0
var float lastBullBreakLow=0

// structure
var int structureLowIndex=0
float structureLow=1000000

// order block drawing arrays
var longBoxes = array.new_box()
var shortBoxes = array.new_box()

var int lastLongIndex=0
var int lastShortIndex=0

BosCandle:=false

var int AlertCondition = 0

AlertCondition:=0


// get the lowest point in the range
structureLow:=lowest(low, range)[1]

// bearish break of structure
if(crossunder(low,structureLow))
    if((bar_index - lastUpIndex) < 1000 and bar_index>lastShortIndex)
        // add bear order block
        array.push(shortBoxes,box.new(left=lastUpIndex, top=lastHigh, bottom=lastUpLow,right=lastUpIndex, bgcolor=bearishOBColour,border_color=bearishOBColour, extend=extend.right))
        // show bos candle
        BosCandle:=true
        // alert if change in direction on break
        if(CandleColourMode==1)
            AlertCondition:=1
        // color mode bear
        CandleColourMode:=0
        // update last short index to prevent duplication
        lastShortIndex:=bar_index
        // alert
        alert("Bearish break in structure",alert.freq_once_per_bar)


// bullish break of structure?
if((array.size(shortBoxes)>0))
    for i = (array.size(shortBoxes)-1) to 0
        box=array.get(shortBoxes,i)
        top=box.get_top(box)
        if(close>top and close>(ema(high,50)))
            // remove the short box 
            box.delete(box)
            array.remove(shortBoxes, i)
            // ok to draw?
            if((bar_index - lastDownIndex) < 1000 and bar_index>lastLongIndex)
                // add bullish order block
                array.push(longBoxes, box.new(left=lastDownIndex, top=lastDown, bottom=lastLow,right=lastDownIndex, bgcolor=bullishOBColour,border_color=bullishOBColour, extend=extend.right))
                // show bos candle
                BosCandle:=true
                // colour mode bullish
                CandleColourMode:=1
                // record last bull bar index to prevent duplication
                lastLongIndex:=bar_index
                lastBullBreakLow:=low
                // alert
                alert("Bullish break in structure",alert.freq_once_per_bar)


// remove LL if close below
if((array.size(longBoxes) > 0))
    for i = (array.size(longBoxes)-1) to 0
        lbox=array.get(longBoxes,i)
        bottom=box.get_bottom(lbox)
        top=box.get_top(lbox)
        if(close<bottom)
            array.remove(longBoxes, i)
            box.delete(lbox)

// candle colouring
CandleColour= CandleColourMode==1?color.lime:color.red
CandleColour:= BosCandle?color.yellow:CandleColour
CandleColour:= close<lastBullBreakLow and CandleColourMode==1?color.orange:CandleColour
barcolor(CandleColour)


// record last up and down candles
if(close<open)
    lastDown:=high
    lastDownIndex:=bar_index
    lastLow:=low

if(close>open)
    lastUp:=close
    lastUpIndex:=bar_index
    lastUpOpen:=open
    lastUpLow:=low
    lastHigh:=high
    
// update last high/low for more accurate order block placements
lastHigh:=high>lastHigh?high:lastHigh
lastLow:=low<lastLow?low:lastLow



